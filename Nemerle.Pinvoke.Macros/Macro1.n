using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Extensions;

using System;
using System.Collections.Generic;
using System.Diagnostics;
using System.IO;
using System.Linq;
using System.Runtime.InteropServices;

using Nemerle.Pinvoke.CParser;
using Nemerle.Pinvoke.CParser.ReflectionTree;

namespace Nemerle.Pinvoke.Macros
{
  [MacroUsage(MacroPhase.BeforeInheritance, MacroTargets.Assembly)]
  macro Include(files : string, params options : list[PExpr])
  {
    
    def manager = Macros.Manager();
    def projectDir = Path.GetDirectoryName(manager.Options.ProjectPath);
    
    def parseExternals(externalsList)
    {
        def parse(pExpr)
        {
            | <[$alias = $type]> =>  (alias.ToString(), type.ToString())
        }

        externalsList.Map(p => parse(p))
    }

    mutable externals;

    foreach(item in options)
    {
        | <[ Externals = [..$externalsList]]> => externals = parseExternals(externalsList)
        | _ => Message.Error("err")
    }
       
    foreach(fileName in Directory.GetFiles(projectDir, files))
    {
        NativeStructImpl.DoTransform(Macros.ImplicitCTX(), fileName, string.Concat(File.ReadAllLines(fileName)), externals )
    }
  }
  
  module NativeStructImpl
  {
    public DoTransform(typer : Typer, _name : string, content : string, _externals : list.[string * string]) : void
    {
      Macros.DefineCTX(typer);

      def parser = DeclarationsParser();
      
      unless (Nemerle.Macros.ImplicitCTX().Manager.IsIntelliSenseMode)
      {
          _ = Debugger.Launch();
      }
      

      def str = parser.Parse(content);
      
      def reflectionTree = str.Map(CParser.TreeConverter.Convert).WithDefault([]);
      
      reflectionTree.Map(entry => Generate(entry).Map(e => typer.Env.Define(e))).Flatten().Iter(b =>b.Compile());
    }

    private Generate(entry : TopLevelEntry) : list[ClassMember.TypeDeclaration]
    {
        | TopLevelEntry.Typedef as typedef  => Generate(typedef.Aliases)
        | TopLevelEntry.Type as type        => Generate(type.Value)
        | TopLevelEntry.Function            => []
    }

    private GetSize(type : ReflectionTree.Type, GetRealType : string -> ReflectionTree.Type) : int
    {
        def DoGetSize(type)
        {        
        | Type.BuiltIn as value => 
            {
                def (_, size) = GetBuiltInTypeInfo(value.Type);
                size
            }
        | Type.Struct  as value => value.Members.Map(f => DoGetSize(f.Type)).Sum()
        | Type.Union   as value => value.Members.Map(f => DoGetSize(f.Type)).Max()
        | Type.Typedef as value => DoGetSize(GetRealType(value.Name))
        | Type.Array   as value => DoGetSize(value.ElementType) * value.Size
        }

        DoGetSize(type)
    }    
    

    private Generate(type : ReflectionTree.Type) : list[ClassMember.TypeDeclaration]
    {
        | Type.Struct   as value    => [GenereateStruct(value)] 
        | Type.Union   
        | Type.BuiltIn 
        | Type.Typedef 
        | Type.Pointer  => []
    }

    

    private GenereateStruct(structType : Type.Struct) : ClassMember.TypeDeclaration
    {
        def name = structType.Name.Value;
        def members = Generate(structType.Members);
        
        <[decl: struct $(name : dyn) { ..$members }]>
    }

    private Generate(fields : list[Field]) : list[ClassMember]
    {
        def convert(field : Field) 
        {
            def name = field.Name;
            def type = GetReferenceName(field.Type);
            def classMember = <[ decl: public mutable $(name : dyn) : $(type);]>;
            
            match(field.Type)
            {
                | Type.Array as arr => classMember.AddCustomAttribute(<[MarshalAs(UnmanagedType.ByValArray, SizeConst = $(arr.Size))]>);
                | _                 => {}
            }
            
            classMember
        }
        
        fields.Map(f => convert(f))
    }
    
    private Generate(aliases : list[TypedefAlias]) : list[ClassMember.TypeDeclaration]
    {
        def convert(typedef : TypedefAlias)
        {
            def typeExpr = GetReferenceName(typedef.Type);
            def typedefNameSliceable = Splicable.Name(<[$(typedef.Name : usesite)]>);
        
            def topDeclaration = TopDeclaration.Alias(typedefNameSliceable,  AttributesAndModifiers(), Typarms.Empty, typeExpr);
            ClassMember.TypeDeclaration(typedefNameSliceable, topDeclaration);
        }
        
        aliases.Map(a => convert(a))
    }
    
    private GetReferenceName(type : ReflectionTree.Type) : PExpr
    {
        | Type.Struct as value    => value.Name.Map(n => <[$(n : dyn)]>).WithDefault(GenerateNameForUnnamed())
        | Type.Union  as value    => value.Name.Map(n => <[$(n : dyn)]>).WithDefault(GenerateNameForUnnamed())
        | Type.BuiltIn as value   => GetBuiltInTypeInfo(value.Type)[0]
        | Type.Typedef as value   => <[ $(value.Name : dyn)]>
        | Type.Pointer as value   => GetReferenceName(value.Pointee)
        | Type.Array as value     => <[array[$(GetReferenceName(value.ElementType))]]>
    }
  
    private GetBuiltInTypeInfo(b: BuiltInType) : PExpr * int
    {
        | BuiltInType.Char
        | BuiltInType.SignedChar
        | BuiltInType.UnsignedChar           => (<[byte  ]>, 1)
        | BuiltInType.Short                  => (<[short ]>, 2)
        | BuiltInType.UnsignedShort          => (<[ushort]>, 2)
        | BuiltInType.Int                    => (<[int   ]>, 4)
        | BuiltInType.UnsignedInt            => (<[uint  ]>, 4)
        | BuiltInType.Long                   => (<[int   ]>, 4)
        | BuiltInType.UnsignedLong           => (<[uint  ]>, 4)
        | BuiltInType.LongLong               => (<[long  ]>, 8)
        | BuiltInType.UnsignedLongLong       => (<[ulong ]>, 8)
        | BuiltInType.Float                  => (<[float ]>, 4)
        | BuiltInType.Double                 => (<[double]>, 8)
        | BuiltInType.Bool                   => (<[bool  ]>, 1)
        | BuiltInType.LongDouble             => (<[double]>, 8)
        | BuiltInType.Void                   => (<[IntPtr]>, 4)
        | BuiltInType.LongDoubleComplex
        | BuiltInType.Complex
        | BuiltInType.FloatComplex
        | BuiltInType.DoubleComplex
        | _                                  => throw NotSupportedException()        
    }
    
    
    GenerateNameForUnnamed() : PExpr.Ref
    {
        <[ unnamed ]>
    }
}
}
