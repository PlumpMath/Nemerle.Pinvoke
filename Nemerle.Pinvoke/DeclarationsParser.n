using Nemerle;
using Nemerle.Collections;
using Nemerle.ComputationExpressions;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Peg;

using System;
using System.Collections.Generic;
using System.Linq;

using Nemerle.Pinvoke.AST;
using Nemerle.Pinvoke.AST.DeclarationSpecifiers;
using Nemerle.Pinvoke.AST.Declarator;
using Nemerle.Pinvoke.AST.Expressions;
using Nemerle.Pinvoke.AST.Identifiers;

namespace Nemerle.Pinvoke
{
  [PegGrammar(Options = EmitDebugSources, declaration,
  grammar
  {
      any                 = ['\u0000'..'\uFFFF'];
      
      #region Line terminators

      newLineCharacter = '\n'
                        / '\r'
                        / '\u2028'    /*  line separator       */
                        / '\u2029';   /*  paragraph separator  */
      newLine   = "\r\n" / newLineCharacter;

      #endregion

      #region White space

      whitespace = [Zs]
                / '\t'
                / '\v'        /*  vertial tab          */
                / '\f';       /*  form feed            */

      #endregion

      #region Spacer

      space = whitespace / newLine;
      
      [InlineAllSubrules]
      s : void = space*;                      /* optional spacer          */
      S : void = !identifierBody s;           /* identifier break spacer  */
      
      #endregion
      
      #region A.1.2 Keywords
      
      keyword = ("auto"           / "break"         / "case"      /
                 "char"           / "const"         / "continue"  /
                 "default"        / "do"            / "double"    /
                 "else"           / "enum"          / "extern"    /
                 "float"          / "for"           / "goto"      /
                 "if"             / "inline"        / "int"       /
                 "long"           / "register"      / "restrict"  /
                 "return"         / "short"         / "signed"    /
                 "sizeof"         / "static"        / "struct"    /
                 "switch"         / "typedef"       / "union"     /
                 "unsigned"       / "void"          / "volatile"  /
                 "while"          / "_Alignas"      / "_Alignof"  /
                 "_Atomic"        / "_Bool"         / "_Complex"  /
                 "_Generic"       / "_Imaginary"    / "_Noreturn" /
                 "_Static_assert" / "_Thread_local" ) !identifierBody;
                 
      #endregion
      
      #region A.1.3 Identifiers
      
      digit            = ['0'..'9'];
      nonDigit		   = ['a'..'z', 'A'..'Z'] / '_' / universalCharacterName;
      identifierBody   = nonDigit (nonDigit / digit)*;
      identifier : Identifier = !keyword identifierBody s;
      
      #endregion
      
      #region A.1.4 Universal character names
      
      hexQuad                = hexadecimalDigit hexadecimalDigit hexadecimalDigit hexadecimalDigit;
      universalCharacterName = "\\u" hexQuad / "\\U" hexQuad hexQuad;
      
      #endregion

      #region A.1.5 Constants
      
      constant        = integerConstant     /
                        floatingConstant    /
                        enumerationConstant /
                        characterConstant;
                        
      integerConstant = decimalConstant integerSuffix? /
                        octalConstant integerSuffix?   /
                        hexadecimalConstant integerSuffix?;
                        
      decimalConstant = nonzeroDigit digit+;
                        
      octalConstant   = '0' octalDigit+;
                        
      hexadecimalConstant = hexadecimalPrefix hexadecimalDigit+;
      
      hexadecimalPrefix   = "0x" / "0X";
      
      nonzeroDigit          = ['1'..'9'];
      
      octalDigit            = ['0'..'7'];
      
      hexadecimalDigit      = ['0'..'9', 'A'..'F', 'a'..'f'];
      
      integerSuffix = unsignedSuffix longSuffix?     /
                      unsignedSuffix longLongSuffix  /
                      longSuffix     unsignedSuffix? /
                      longLongSuffix unsignedSuffix?;
                      
      unsignedSuffix = 'u' / 'U';
      
      longSuffix     = 'l' / 'L';
      
      longLongSuffix = "ll" / "LL";
      
      floatingConstant = decimalFloatingConstant /
                         hexadecimalFloatingConstant;
                         
      decimalFloatingConstant = fractionalConstant exponentPart? floatingSuffix?
                                digitSequence exponentPart floatingSuffix?;
                                
      hexadecimalFloatingConstant = hexadecimalPrefix hexadecimalFractionalConstant binaryExponentPart floatingSuffix? /
                                    hexadecimalPrefix hexadecimalDigitSequence binaryExponentPart floatingSuffix?;
                                    
      fractionalConstant         = digitSequence? '.' digitSequence / 
                                   digitSequence '.';
                                   
      exponentPart               = 'e' sign? digitSequence /
                                   'E' sign? digitSequence;
                                   
      sign                       = '+' / 
                                   '-';
                                   
      digitSequence              =  digit+;
      
      hexadecimalFractionalConstant = hexadecimalDigitSequence? '.' hexadecimalDigitSequence /
                                      hexadecimalDigitSequence '.';
                                      
      binaryExponentPart = 'p' sign? digitSequence /
                           'P' sign? digitSequence;
                           
      hexadecimalDigitSequence = hexadecimalDigit+;
      
      floatingSuffix = 'f' / 'l' / 'F' / 'L';
      
      enumerationConstant : Identifier = identifier;
      
      characterConstant     = "'" cCharSequence "'"  /
                              "L'" cCharSequence "'" /
                              "u'" cCharSequence "'" /
                              "U'" cCharSequence "'";
                              
      cCharSequence = cChar+;
      
      cChar = !(newLineCharacter / '\'' / '\\') any / escapeSequence;
      
      escapeSequence        = simpleEscapeSequence      /
                              octalEscapeSequence       /
                              hexadecimalEscapeSequence /
                              universalCharacterName;
                              
      simpleEscapeSequence = '\\' ('\'' / '"' / '?' / '\\' / 'a' / 'b' / 'f' / 'n' / 'r' / 't' / 'v');
      
      octalEscapeSequence       = '\\' octalDigit octalDigit? octalDigit?;
      
      hexadecimalEscapeSequence = "\\x" hexadecimalDigit+;
      
      #endregion
      
      #region A.1.6 String literals
      
      stringLiteral = encodingPrefix? '"' sCharSequence '"';
      
      encodingPrefix = "u8" /
                       "u"  /
                       "U"  /
                       "L";
      
      sCharSequence = sChar*;
      
      sChar = !(newLineCharacter / '"' / '\\') any / escapeSequence / universalCharacterName;
      
      #endregion
      
      #region A.2.1 Expressions
      
      primaryExpression = identifier           /
                          constant             /
                          stringLiteral        /
                          '('s expression ')'s /
                          genericSelection;
                          
      genericSelection = "_Generic"s '('s assignmentExpression ','s genericAssocList ')'s;
      
      genericAssocList = genericAssociation (','s genericAssociation)*;
      
      genericAssociation = typeName ':'s assignmentExpression /
                           "default"s ':'s assignmentExpression;
      
      postfixExpressionTail = '['s expression ']'s  /
                              argumentExpressionList? /
                              '.'s identifier        /
                              "->"s identifier       /
                              "++"                    /
                              "--"; 
      postfixExpression = primaryExpression / ('('s typeName ')'s '{'s initializerList (','s)? '}'s) postfixExpressionTail*;
                         
      argumentExpressionList = assignmentExpression (','s assignmentExpression)*;
      
      unaryExpression = postfixExpression               /
                        "++"s unaryExpression          /
                        "--"s unaryExpression          /
                        unaryOperator castExpression    /
                        "sizeof"s unaryExpression      /
                        "sizeof"s '('s typeName ')'s /
                        "_Alignof"s '('s typeName ')'s;
                        
      unaryOperator = '&' / '*' / '+' / '-' / '~' / '!';
      
      castExpression = unaryExpression /
                       '('s typeName ')'s castExpression;
      
      multiplicativeExpressionTail = '*'s castExpression /
                                     '/'s castExpression /
                                     '%'s castExpression;
      multiplicativeExpression     = castExpression multiplicativeExpressionTail*;
      
      additiveExpressionTail       = '+'s multiplicativeExpression /
                                     '-'s multiplicativeExpression;
      additiveExpression           = multiplicativeExpression additiveExpressionTail*;
      
      shiftExpressionTail          = "<<"s additiveExpression /
                                     ">>"s additiveExpression;
      shiftExpression              = additiveExpression shiftExpressionTail*;
      
      relationalExpressionTail     = '<'s shiftExpression  /
                                     '>'s shiftExpression  /
                                     "<="s shiftExpression /
                                     ">="s shiftExpression;
      relationalExpression        = shiftExpression relationalExpressionTail*;
      
      equalityExpressionTail      = "=="s relationalExpression /
                                    "!="s relationalExpression;
      equalityExpression          = relationalExpression equalityExpressionTail*;
      
      andExpression               = equalityExpression ('&'s equalityExpression)*;
      
      exclusiveOrExpression       = andExpression ('^'s andExpression)*;
      
      inclusiveOrExpression       = exclusiveOrExpression ('|'s exclusiveOrExpression)*;
      
      logicalAndExpression        = inclusiveOrExpression ("&&"s inclusiveOrExpression)*;

      logicalOrExpression         = logicalAndExpression ("||"s logicalAndExpression)*;
      
      conditionalExpression       = logicalOrExpression ("?"s expression ":"s conditionalExpression)*;
      
      assignmentExpression        = conditionalExpression /
                                    unaryExpression assignmentOperator assignmentExpression;
                                 
      assignmentOperator = "=" / "*=" / "/=" / "%=" / "+=" / "-=" / "<<=" / ">>=" / "&=" / "^=" / "|=";
      
      expression = assignmentExpression (','s assignmentExpression)*;
      
      constantExpression : ConstantExpression = conditionalExpression;
            
      #endregion
      
      #region A.2.2 Declarations

      normalDeclaration : Declaration = declarationSpecifiers initDeclaratorList? ';'s;
      
      declaration : Declaration =   normalDeclaration  /
                                    staticAssertDeclaration;
      
      declarationSpecifiers : list[DeclarationSpecifier]  =   (storageClassSpecifier                /
                                                               typeDeclarationSpecifier             /
                                                               typeQualifeierDeclarationSpecifier   /
                                                               functionDeclarationSpecifier         /
                                                               alignmentDeclarationSpecifier)+;
                              
      initDeclaratorList : list[InitDeclarator]   = initDeclarator (','s initDeclarator)*;
      
      normalInitDeclarator : InitDeclarator = declarator;
      initDeclaratorWithInitializer : InitDeclarator= declarator '='s initializer;
      
      initDeclarator : InitDeclarator         = normalInitDeclarator /
                                                initDeclaratorWithInitializer;
                              
      storageClassSpecifier : DeclarationSpecifier = 
                                                "typedef"       /
                                                "extern"        /
                                                "static"        /
                                                "_Thread_local" /
                                                "auto"          /
                                                "register";
      
      builtInTypeSpecifier : DeclarationSpecifier  = 
                                                "void"           / 
                                                "char"           / 
                                                "short"          / 
                                                "int"            / 
                                                "long"           /
                                                "float"          /
                                                "double"         /
                                                "signed"         /
                                                "unsigned"       /
                                                "_Bool"          /
                                                "_Complex";
      
      structOrUnionTypeSpecifier : DeclarationSpecifier = structOrUnionSpecifier;

      enumTypeSpecifier : TypeSpecifier = enumSpecifier;
      typedefTypeSpecifier : TypeSpecifier = typedefName;
      atomicTypeSpecifier : TypeSpecifier = "_Atomic"s '('s typeName ')'s;


      typeSpecifier : TypeSpecifier  =
                              atomicTypeSpecifier       /
                              enumTypeSpecifier         /
                              typedefTypeSpecifier;
      
      typeDeclarationSpecifier : DeclarationSpecifier = typeSpecifier;
      
      forwardDeclarationStructOrUnionSpecifier : StructOrUnionSpecifier = structOrUnion identifier;
      declarationStructOrUnionSpecifier        : StructOrUnionSpecifier = (structOrUnion identifier? '{'s structDeclarationList '}'s);
      structOrUnionSpecifier                   : StructOrUnionSpecifier = forwardDeclarationStructOrUnionSpecifier / 
                                                                          declarationStructOrUnionSpecifier;
      
      structOrUnion : StructOrUnion   = ("struct" / "union")S;
      
      structDeclarationList : list[StructDeclaration] = structDeclaration+;
      
      normalStructDeclaration : StructDeclaration = specifierQualifierList structDeclaratorList? ';'s;
            
      structDeclaration  : StructDeclaration = normalStructDeclaration /
                                               staticAssertStructDeclaration;

      typeSpecifierSpecifierQualifierList : list[DeclarationSpecifier] = typeSpecifier specifierQualifierList?;
      typeQualifierSpecifierQualifierList : list[DeclarationSpecifier] = typeQualifier specifierQualifierList;

      specifierQualifierList : list[DeclarationSpecifier] = typeSpecifierSpecifierQualifierList / typeQualifierSpecifierQualifierList;


                               
      structDeclaratorList   : list[StructDeclarator] = structDeclarator (','s structDeclarator)*;
      
      normalStructDeclarator : StructDeclarator = declarator;
      bitFieldStructDeclarator : StructDeclarator = declarator? ':'s constantExpression;

      structDeclarator       = normalStructDeclarator / bitFieldStructDeclarator;

      normalEnumSpecifier : EnumSpecifier = "enum"S identifier? '{'s enumeratorList '}'s;
      enumSpecifierWithComma : EnumSpecifier = "enum"S identifier? '{'s enumeratorList ','s '}'s;
      forwardDeclarationEnumSpecififer : EnumSpecifier = "enum"S identifier;
                               
      enumSpecifier : EnumSpecifier 
                                   =   normalEnumSpecifier /
                                       enumSpecifierWithComma /
                                       forwardDeclarationEnumSpecififer;
      
      enumeratorList : list[Enumerator] = enumerator (','s enumerator)*;
      
      normalEnumerator              : Enumerator = enumerationConstant;
      enumeratorWithExplicitValue   : Enumerator = enumerationConstant '='s constantExpression;
      enumerator                    : Enumerator = normalEnumerator / enumeratorWithExplicitValue;
     
      typeQualifier : TypeQualifier 
                                      = "const"    / 
                                        "restrict" /
                                        "volatile" /
                                        "_Atomic";

      typeQualifeierDeclarationSpecifier    : DeclarationSpecifier  = typeQualifier;
      functionSpecifier                     : FunctionSpecifier     = "inline" / "_Noreturn";
      functionDeclarationSpecifier          : DeclarationSpecifier  = functionSpecifier;

      expressionAlignmentSpecifier          : AlignmentSpecifier = '('s constantExpression ')'s;
      alignAsAlignmentSpecifier             : AlignmentSpecifier = '('s typeName ')'s;
      alignmentSpecifier                    : AlignmentSpecifier =  "_Alignas"s (alignAsAlignmentSpecifier / expressionAlignmentSpecifier);
      alignmentDeclarationSpecifier         : DeclarationSpecifier = alignmentSpecifier;
      
      declarator : Declarator   = pointer? directDeclarator;
      
      simpleDirectDeclaratorHead : DirectDeclaratorHead = identifier;
      recurrentDirectDeclaratorHead  : DirectDeclaratorHead = '('s declarator ')'s;
      
      directDeclaratorHead : DirectDeclaratorHead = simpleDirectDeclaratorHead / recurrentDirectDeclaratorHead;

      assignmentExpressionDirectDeclaratorTailElement : DirectDeclaratorTailElement         = '['s  typeQualifierList? assignmentExpression? ']'s;
      static1AssignmentExpressionDirectDeclaratorTailElement : DirectDeclaratorTailElement  = '['s "static"s typeQualifierList? assignmentExpression ']'s;
      static2AssignmentExpressionDirectDeclaratorTailElement : DirectDeclaratorTailElement  = '['s typeQualifierList "static"s assignmentExpression ']'s;
      asterikDirectDeclaratorTailElement : DirectDeclaratorTailElement                      = '['s typeQualifierList? '*'s ']'s;
      FunctionWithParametersDirectDeclaratorTailElement : DirectDeclaratorTailElement       = '('s parameterTypeList ')'s;
      FunctionWithIdentifiersDirectDeclaratorTailElement : DirectDeclaratorTailElement      = '('s identifierList? ')'s; 

      directDeclaratorTailElement : DirectDeclaratorTailElement = 
        assignmentExpressionDirectDeclaratorTailElement         /
        static1AssignmentExpressionDirectDeclaratorTailElement  /
        static2AssignmentExpressionDirectDeclaratorTailElement  /
        asterikDirectDeclaratorTailElement                      /   
        FunctionWithParametersDirectDeclaratorTailElement       /
        FunctionWithIdentifiersDirectDeclaratorTailElement;
        
      directDeclarator : DirectDeclarator = directDeclaratorHead directDeclaratorTailElement*;
      
      normalPointer         : Pointer = '*'s typeQualifierList?;
      recurrentPointer      : Pointer = '*'s typeQualifierList? pointer;

      pointer : Pointer = normalPointer / recurrentPointer;
                
      typeQualifierList : list[TypeQualifier] = typeQualifier+;
      
      parameterTypeList                 : list[ParameterDeclaration] * bool =  parameterList (','s "..."s)?;
      parameterList                     : list[ParameterDeclaration] =  parameterDeclaration (','s parameterDeclaration)*;
      
      normalParameterDeclaration        : ParameterDeclaration = declarationSpecifiers declarator;
      abstractParameterDeclaration      : ParameterDeclaration = declarationSpecifiers abstractDeclarator?;
      parameterDeclaration              : ParameterDeclaration = normalParameterDeclaration / abstractParameterDeclaration;
      
      identifierList : list[Identifier] = identifier (','s identifier)*;
      
      typeName  : TypeName     = specifierQualifierList abstractDeclarator?;
 
      singularPointerAbstractDeclarator : AbstractDeclarator = pointer;
      normalAbstractDeclarator : AbstractDeclarator = pointer? directAbstractDeclarator;
      
      abstractDeclarator : AbstractDeclarator = singularPointerAbstractDeclarator / normalAbstractDeclarator;

      
      directAbstractDeclaratorTailElement : DirectAbstractDeclaratorTailElement = 
        assignmentExpressionDirectAbstractDeclaratorTailElement         /
        static1AssignmentExpressionDirectAbstractDeclaratorTailElement  /
        static2AssignmentExpressionDirectAbstractDeclaratorTailElement  /
        asterikDirectAbstractDeclaratorTailElement                      /
        FunctionWithParametersDirectAbstractDeclaratorTailElement;
      
      assignmentExpressionDirectAbstractDeclaratorTailElement        : DirectAbstractDeclaratorTailElement = '['s typeQualifierList? assignmentExpression? ']'s;
      static1AssignmentExpressionDirectAbstractDeclaratorTailElement : DirectAbstractDeclaratorTailElement = '['s "static"s typeQualifierList? assignmentExpression ']'s;
      static2AssignmentExpressionDirectAbstractDeclaratorTailElement : DirectAbstractDeclaratorTailElement = '['s typeQualifierList "static"s assignmentExpression ']'s;
      asterikDirectAbstractDeclaratorTailElement                     : DirectAbstractDeclaratorTailElement = '['s '*'s ']'s;
      FunctionWithParametersDirectAbstractDeclaratorTailElement      : DirectAbstractDeclaratorTailElement = '('s parameterTypeList? ')'s;

      normalDirectAbstractDeclarator    : DirectAbstractDeclarator = '('s abstractDeclarator ')' directAbstractDeclaratorTailElement*;
      singularDirectAbstractDeclarator  : DirectAbstractDeclarator = directAbstractDeclaratorTailElement+;
      directAbstractDeclarator          : DirectAbstractDeclarator = normalDirectAbstractDeclarator/singularDirectAbstractDeclarator;  
                       
      typedefName    = identifier;
      
      initializer    = assignmentExpression      / 
                       '{'s initializerList '}'s /
                       '{'s initializerList ','s '}'s;
      
      initializerListElement = designation? initializer;
      initializerList        = initializerListElement (','s initializerListElement)*;
                        
      designation = designatorList '='s;
      
      designatorList = designator+;
      
      designator = '['s constantExpression ']'s /
                    '.'s identifier;
                    
      staticAssertStructDeclaration : StructDeclaration = staticAssertDeclaration;
      staticAssertDeclaration       : Declaration       = "_Static_assert"s '('s constantExpression ','s stringLiteral ')'s;
      
      #endregion
      
      #region A.2.3 Statements
      
      statement = labeledStatement    /
                  compoundStatement   /
                  expressionStatement /
                  selectionStatement  /
                  iterationStatement  /
                  jumpStatement;
                  
      labeledStatement = identifier ':'s statement                 /
                         "case"s constantExpression ':'s statement /
                         "default"s ':'s statement;
                         
      compoundStatement = '{'s blockItemList? '}'s;
      
      blockItemList     = blockItem+;
      blockItem         = declaration /
                          statement;
                          
      expressionStatement = expression? ';'s;
      
      selectionStatement  = "if"s '('s expression ')'s statement ("else"s statement)? /
                            "switch"s '('s expression ')'s statement;
                            
      iterationStatement  = "while"s '('s expression ')'s statement                                  /
                            "do"s statement "while"s '('s expression ')'s ';'s                       /
                            "for"s '('s expression? ';'s expression? ';'s expression? ')'s statement /
                            "for"s '('s declaration expression? ';'s expression? ')'s statement;
                            
      jumpStatement     = "goto"S identifier ';'s /
                          "continue"s ';'s        /
                          "break"s ';'s           /
                          "return"s expression? ';'s;

      #endregion
      
      #region A.2.4 External definitions
      
      translationUnit     = externalDeclaration+;
                        
      externalDeclaration = functionDefinition / declaration;
      
      functionDefinition  = declarationSpecifiers declarator declarationList? compoundStatement;
      
      declarationList     = declaration+;
      
      #endregion
  })]
  public partial class DeclarationsParser
  {
     typeName(_ : list[DeclarationSpecifier], _ : option[AbstractDeclarator]) : TypeName
     {
        TypeName("")
     }
  }
}
